# Maintainer: Cody Schafer <aur at codyps com>
# Maintainer: dequis <dx@dxzone.com.ar>
# Previous maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=rr-git
pkgver=5.4.0.r315.g1817b9d4
pkgrel=1
pkgdesc='Record and Replay framework: lightweight recording and deterministic debugging'
arch=(i686 x86_64)
url='http://rr-project.org/'
license=('custom')
depends=('gdb' 'capnproto' 'lib32-gcc-libs')
makedepends=('git' 'cmake' 'gdb' 'ninja')
conflicts=('rr')
options=(!strip)

source=(
    "${pkgname}::git+https://github.com/rr-debugger/rr"
    "zen_workaround.service"
)
sha256sums=('SKIP'
            '0ecbb03794d55fdd1759bed5bcc1d5ba490ca02cd7906eb22438596eb38942d7')

prepare() {
	cd "${pkgname}"
	mkdir -p build
}

build() {
	cd "${pkgname}/build"
	cmake \
		-GNinja \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DBUILD_TESTS=OFF \
		-DWILL_RUN_TESTS=OFF \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_CXX_STANDARD=14 \
		..

	cmake --build .	 -- -v
}

pkgver() {
    cd "${pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
	cd "${pkgname}"/build
	DESTDIR="${pkgdir}" cmake --build . -- -v install
	cd ..
	install -D LICENSE "${pkgdir}/usr/share/licenses/rr/LICENSE"
    install -Dm644 ../zen_workaround.service "${pkgdir}/usr/lib/systemd/system/zen_workaround.service"
    install -Dm755 scripts/zen_workaround.py "${pkgdir}/usr/share/zen_workaround/zen_workaround.py"
}
